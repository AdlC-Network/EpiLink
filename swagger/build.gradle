plugins {
    id 'org.hidetake.swagger.generator'
}

configurations {
    swagger {
        canBeConsumed = true
        canBeResolved = false
    }
}

repositories {
    mavenCentral()
}

dependencies {
    swaggerUI libs.swaggerUi
}

swaggerSources {
    epilink {
        inputFile = file('epilink.yaml')
        // From the README of the plugin, this injects a custom index.html
        ui {
            doLast {
                copy {
                    from 'index.html'
                    into outputDir
                }
            }
        }
    }
}

artifacts {
    swagger(generateSwaggerUIEpilink.outputDir) {
        builtBy(generateSwaggerUIEpilink)
    }
}

// Properly rebuild when the index.html changes
tasks.generateSwaggerUIEpilink.inputs.file("index.html")
