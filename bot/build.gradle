plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    main.kotlin.srcDirs = ["src"]
    main.resources.srcDirs = ["resources"]
    test.kotlin.srcDirs = ["test"]
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
}

compileTestKotlin {
    kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
}

application {
    mainClassName = "org.epilink.bot.MainKt"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-scripting-common:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-scripting-jvm:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-scripting-jvm-host-embeddable:$kotlinVersion"

    implementation "io.ktor:ktor-server-netty:$ktorVersion"
    implementation "io.ktor:ktor-auth:$ktorVersion"
    implementation "io.ktor:ktor-jackson:$ktorVersion"
    implementation "io.ktor:ktor-client-apache:$ktorVersion"
    implementation "io.ktor:ktor-client-auth-jvm:$ktorVersion"

    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "org.jetbrains.exposed:exposed-core:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-dao:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-java-time:$exposedVersion"
    implementation "org.xerial:sqlite-jdbc:3.30.1"

    implementation "com.xenomachina:kotlin-argparser:2.0.7"

    implementation "com.discord4j:discord4j-core:3.0.12"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactor:1.3.5"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:1.3.5"

    implementation "org.koin:koin-core:2.1.5"
    implementation "org.koin:koin-logger-slf4j:2.1.5"

    implementation "io.lettuce:lettuce-core:5.2.2.RELEASE"

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlinVersion"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.1'
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "org.koin:koin-test:2.1.5"
    testImplementation "io.ktor:ktor-server-test-host:$ktorVersion"
    testImplementation "io.ktor:ktor-client-mock-jvm:$ktorVersion"
}

test {
    useJUnitPlatform()
}

import java.nio.file.FileAlreadyExistsException
import java.nio.file.Files
import java.nio.file.Paths

// Front-end bootstrapping support
// Can be enabled via -PwithFrontend

task bootstrapFrontEnd {
    dependsOn 'bundleFrontEndIntoResources'
    doLast {
        try {
            Files.createFile(Paths.get("$buildDir/resources/main/.hasFrontend"))
        } catch (FileAlreadyExistsException ignored) {
        }
    }
}

task bundleFrontEndIntoResources(type: Copy) {
    dependsOn ':web:bundleWeb'
    mustRunAfter 'processResources'
    from '../web/build/web'
    into 'build/resources/main/frontend'
    duplicatesStrategy DuplicatesStrategy.INCLUDE // overwrite
}

task cleanFrontEnd(type: Delete) {
    delete "$buildDir/resources/main/frontend", "$buildDir/resources/main/.hasFrontend"
}

if (project.hasProperty("withFrontend")) {
    classes.dependsOn 'bootstrapFrontEnd'
} else {
    classes.dependsOn 'cleanFrontEnd'
}