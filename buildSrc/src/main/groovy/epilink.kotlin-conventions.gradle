plugins {
    id "org.jetbrains.kotlin.jvm"
    id "io.gitlab.arturbosch.detekt"
    id 'java'
    id 'jacoco'
    id 'epilink.common-conventions'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://litarvan.github.io/maven' }
}

def additionalKotlinCompilerArgs = [
        "-Xopt-in=kotlin.RequiresOptIn",
        "-Werror" // All warnings as errors
]

compileKotlin {
    kotlinOptions.jvmTarget = "17"
    kotlinOptions.freeCompilerArgs += additionalKotlinCompilerArgs
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "17"
    kotlinOptions.freeCompilerArgs += additionalKotlinCompilerArgs
}

dependencies {
    implementation libs.kotlin.stdlib

    testImplementation libs.bundles.kotlin.test
    testImplementation libs.junit.jupiter
    testImplementation libs.mockk

    detektPlugins libs.detekt.formatting

    // Workaround for detekt/detekt#4287 (see here: https://github.com/detekt/detekt/issues/4287#issuecomment-981657386)
    detekt libs.kotlin.compiler.embeddable
    detekt libs.detekt.cli
}

test {
    useJUnitPlatform()
}

// TODO Remove this when the default JaCoCo version is updated
jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt) {
    jvmTarget = "17"
}

check.configure {
    dependsOn = dependsOn.findAll { it.name != 'detekt' }
    dependsOn += 'detektMain'
}

project.archivesBaseName = "epilink-backend-${project.name}"